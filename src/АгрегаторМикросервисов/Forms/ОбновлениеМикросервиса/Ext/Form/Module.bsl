
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("УИДТекущегоМикросервиса") Тогда 
		УИДТекущегоМикросервиса = Параметры.УИДМикросервиса;	
	КонецЕсли; 
	
	ОбновитьНаСервере(); 
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры  


&НаСервере
Процедура ОбновитьНаСервере()   
	
	ОбрОбъект = РеквизитФормыВЗначение("Объект"); 
	ОбрОбъект.Инициализация();    
	ОбрОбъект.ЗаполнитьМикросервис(ОбрОбъект.УИДТекущегоМикросервиса);
	ЗначениеВРеквизитФормы(ОбрОбъект,"Объект");  
	
	//переносим в форму значения Микросервиса
	ЗаполнитьЗначенияСвойств(ЭтаФорма,ОбрОбъект,"Наименование,ИспользуютсяТокеныДоступа,ИмяПродукта,УИДТекущегоМикросервиса");
	Для Каждого Стр Из ОбрОбъект.ВерсииМикросервиса Цикл
		НовСтр = ВерсииМикросервиса.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр,Стр);
	КонецЦикла;   
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры     

&НаСервере
Процедура УстановитьВидимостьДоступность()   
	
	Элементы.ВерсииМикросервисаУИДВерсии.Видимость = Объект.РежимОтладки;
	
	ОбрОбъект = РеквизитФормыВЗначение("Объект");
	ТекущаяВерсияМикросервиса = ОбрОбъект.ТекущаяВерсияМикросервиса();
	
	ОтметитьДоступныеДляТекущейКонфигурации();            
	
	Элементы.Установить.Доступность = Ложь;
	Для Каждого Стр Из ВерсииМикросервиса Цикл
		Если Стр.ДоступноОбновление Тогда 
			Элементы.Установить.Доступность = Истина;
		КонецЕсли;  
	КонецЦикла;     

	НоваяВерсия = "";
	Для Каждого Стр Из ВерсииМикросервиса Цикл
		Если Стр.ДоступноОбновление Тогда 
			НоваяВерсия = Стр.ВерсияМикросервиса;
			Прервать; 
		КонецЕсли;
	КонецЦикла; 
	
	Элементы.ОбновлениеИнфо.Заголовок = "Доступно обновление для "+Строка(Наименование)+" Версия:"+НоваяВерсия+" (текущая версия "+Строка(ТекущаяВерсияМикросервиса)+")";
	Элементы.Токен.Видимость = ИспользуютсяТокеныДоступа;

КонецПроцедуры 

&НаСервере
Процедура ОтметитьДоступныеДляТекущейКонфигурации() 
	
	ОбрОбъект = РеквизитФормыВЗначение("Объект");
	Для Каждого Стр Из ВерсииМикросервиса Цикл
		Стр.Скачать = "Скачать";
		
		Если (СравнитьВерсии(Объект.ТекущаяВерсия1С,Стр.ВерсияКонфигурацииОт) >= 0 ИЛИ Стр.ВерсияКонфигурацииОт="")
			И (ОбщегоНазначенияКлиентСервер.СравнитьВерсии(Стр.ВерсияКонфигурацииДо,Объект.ТекущаяВерсия1С) >= 0 ИЛИ Стр.ВерсияКонфигурацииДо="") Тогда 
			Стр.Установить = "Установить";
		КонецЕсли; 
		
		Если ОбрОбъект.ТолькоЧисла(Стр.ВерсияМикросервиса)=ТекущаяВерсияМикросервиса И ЗначениеЗаполнено(ТекущаяВерсияМикросервиса) Тогда 
			Стр.Установлена = Истина;
		КонецЕсли;
							
	КонецЦикла; 
	
	Для Каждого Стр Из ВерсииМикросервиса Цикл
		Если Стр.Установить = "Установить" И СравнитьВерсии(Стр.ВерсияМикросервиса,ТекущаяВерсияМикросервиса)>0 Тогда 
			Стр.ДоступноОбновление = Истина;
			Прервать; 
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры    

&НаСервере
Функция СравнитьВерсии(СтрокаВерсии1, СтрокаВерсии2)
	
	ОбрОбъект = РеквизитФормыВЗначение("Объект");
	Возврат ОбрОбъект.СравнитьВерсии(СтрокаВерсии1, СтрокаВерсии2)
	
КонецФункции

&НаКлиенте
Процедура ВерсииМикросервисаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)     
	
	ТекДанные = Элементы.ВерсииМикросервиса.ТекущиеДанные;
	Если ТекДанные=Неопределено Тогда 
		Возврат;
	КонецЕсли;
		
	Если Поле.Имя = "ВерсииМикросервисаСкачать" И ТекДанные.Скачать = "Скачать" Тогда
		СкачатьВерсиюПродукта(ТекДанные.УИДВерсии);
	ИначеЕсли Поле.Имя = "ВерсииМикросервисаУстановить" И ТекДанные.Установить = "Установить" Тогда
		УстановитьВерсиюПродукта(ТекДанные.УИДВерсии,ТекДанные.ВерсияМикросервиса); 		
	КонецЕсли;    
	
КонецПроцедуры

&НаКлиенте
Процедура СкачатьВерсиюПродукта(УИДВерсии)  
	
	Если ИспользуютсяТокеныДоступа И НЕ ЗначениеЗаполнено(Токен) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не заполнен Токен",,"Токен","Токен");
		Возврат;
	КонецЕсли;
	
	АдресХранилища = СкачатьВерсиюПродуктаСервер(УИДВерсии);
	
	Если АдресХранилища = Неопределено Тогда	
		 Возврат;	
	КонецЕсли;  
	
	МассивФайлов = ПолучитьИзВременногоХранилища(АдресХранилища);  
	Для Каждого Эл Из МассивФайлов Цикл
		ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		ДиалогВыбораФайла.ПолноеИмяФайла = Эл.ИмяФайла;
		ДиалогВыбораФайла.Расширение = ПолучитьРасширениеФайла(Эл.ИмяФайла);  
		АдресДД = ПоместитьВоВременноеХранилище(Эл.ДвоичныеДанные);
		ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_ПриЗакрытииВыбораФайла", ЭтотОбъект, АдресДД);
		ДиалогВыбораФайла.Показать(ОписаниеОповещения);
	КонецЦикла;
	
КонецПроцедуры  

&НаКлиенте
Процедура Подключаемый_ПриЗакрытииВыбораФайла(ВыбранныеФайлы, ДопПараметры) Экспорт
	
	Если НЕ ВыбранныеФайлы = Неопределено Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(ДопПараметры);
	    Попытка
			ДвоичныеДанные.Записать(ВыбранныеФайлы[0]);
		Исключение
			Сообщить(ОписаниеОшибки());
		КонецПопытки;  	
	КонецЕсли; 	
	
КонецПроцедуры

&НаСервере
Функция СкачатьВерсиюПродуктаСервер(УИДВерсии)
	
	ОбрОбъект = РеквизитФормыВЗначение("Объект");
	Возврат ОбрОбъект.СкачатьВерсиюПродуктаСервер(УИДТекущегоМикросервиса,УИДВерсии,Токен);
	
КонецФункции   

&НаКлиенте
Процедура УстановитьВерсиюПродукта(УИДВерсии, ВерсияМикросервиса)  
	
	Если ИспользуютсяТокеныДоступа И НЕ ЗначениеЗаполнено(Токен) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не заполнен Токен",,"Токен","Токен");
		Возврат;
	КонецЕсли;

	АдресХранилища = СкачатьВерсиюПродуктаСервер(УИДВерсии);
	
	Если АдресХранилища = Неопределено Тогда	
		 Возврат;	
	КонецЕсли;  
	
	МассивФайлов = ПолучитьИзВременногоХранилища(АдресХранилища);  
	Для Каждого Эл Из МассивФайлов Цикл
		Расширение = ПолучитьРасширениеФайла(Эл.ИмяФайла);  
		АдресДД = ПоместитьВоВременноеХранилище(Эл.ДвоичныеДанные);
		УстановитьВерсиюПродуктаНаСервере(АдресДД, Эл.ИмяФайла, ВерсияМикросервиса);	
		ПоказатьОповещениеПользователя("Версия обновлена. Для применения новой версии необходимо перезапустить Агрегатор микросервисов из дополнительных обработок",,,,СтатусОповещенияПользователя.Важное);
	КонецЦикла; 
	
	//УстановитьВидимостьДоступность(); 
	
КонецПроцедуры 

&НаСервере
Процедура УстановитьВерсиюПродуктаНаСервере(АдресХранилища, ИмяФайла, ВерсияМикросервиса)  
	
	ОбрОбъект = РеквизитФормыВЗначение("Объект");
	ОбрОбъект.УстановитьВерсиюПродукта(АдресХранилища, ИмяФайла, ВерсияМикросервиса);
	 
КонецПроцедуры  

&НаСервере
Функция ПолучитьРасширениеФайла(ИмяФайла)

	ПозицияПоследнейТочки = 0;
	РасширениеФайла = ИмяФайла;

	Пока Истина Цикл 
		ПозицияПоследнейТочки = Найти(РасширениеФайла, "."); 
		Если ПозицияПоследнейТочки = 0 Тогда   
			Прервать; 
		Иначе   
			РасширениеФайла = Сред(РасширениеФайла, ПозицияПоследнейТочки + 1)
		КонецЕсли;
	КонецЦикла;

	Возврат ?(РасширениеФайла = ИмяФайла, "", РасширениеФайла);

КонецФункции 

&НаКлиенте
Процедура Установить(Команда)
	
	Для Каждого Стр Из ВерсииМикросервиса Цикл
		Если Стр.ДоступноОбновление Тогда 
			УстановитьВерсиюПродукта(Стр.УИДВерсии, Стр.ВерсияМикросервиса);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть();
	
КонецПроцедуры